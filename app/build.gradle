plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
}

def releaseStoreFile = project.findProperty("RELEASE_STORE_FILE")
def releaseStorePassword = project.findProperty("RELEASE_STORE_PASSWORD")
def releaseKeyAlias = project.findProperty("RELEASE_KEY_ALIAS")
def releaseKeyPassword = project.findProperty("RELEASE_KEY_PASSWORD")
def isReleaseTaskRequested = gradle.startParameter.taskNames.any { it.toLowerCase().contains("release") }

android {
    namespace 'com.gridee.parking'
    compileSdk 35

    defaultConfig {
        applicationId "com.gridee.parking"
        minSdk 24
        targetSdk 35
        versionCode 3
        versionName "1.0.2"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            if (releaseStoreFile) {
                storeFile file(releaseStoreFile)
            }
            if (releaseStorePassword) {
                storePassword releaseStorePassword
            }
            if (releaseKeyAlias) {
                keyAlias releaseKeyAlias
            }
            if (releaseKeyPassword) {
                keyPassword releaseKeyPassword
            }
        }
    }

    buildTypes {
        release {
            if (isReleaseTaskRequested && (!releaseStoreFile || !releaseStorePassword || !releaseKeyAlias || !releaseKeyPassword)) {
                throw new GradleException("Release signing properties are missing. Set RELEASE_STORE_FILE, RELEASE_STORE_PASSWORD, RELEASE_KEY_ALIAS, RELEASE_KEY_PASSWORD in gradle.properties or via -P.")
            }
            signingConfig signingConfigs.release
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    
    kotlinOptions {
        jvmTarget = '1.8'
    }
    
    buildFeatures {
        viewBinding true
        compose true
    }
    
    composeOptions {
        kotlinCompilerExtensionVersion = '1.5.0'
    }
}

dependencies {
    implementation 'androidx.core:core-ktx:1.12.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.7.0'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.7.0'
    implementation 'androidx.activity:activity-ktx:1.8.2'
    
    // Jetpack Compose BOM
    implementation platform('androidx.compose:compose-bom:2023.10.01')
    implementation 'androidx.compose.ui:ui'
    implementation 'androidx.compose.ui:ui-graphics'
    implementation 'androidx.compose.ui:ui-tooling-preview'
    implementation 'androidx.compose.material3:material3'
    implementation 'androidx.activity:activity-compose:1.8.2'
    implementation 'androidx.compose.runtime:runtime-livedata'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-compose:2.7.0'
    
    // System UI Controller for window insets
    implementation 'com.google.accompanist:accompanist-systemuicontroller:0.32.0'
    
    // Retrofit for API calls
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation 'com.squareup.retrofit2:converter-scalars:2.9.0'
    // Explicit Gson (optional but ensures version alignment)
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.squareup.okhttp3:logging-interceptor:4.12.0'
    
    // Coroutines
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.7.3'
    
    // SwipeRefreshLayout
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    
    // Navigation
    implementation 'androidx.navigation:navigation-fragment-ktx:2.7.5'
    implementation 'androidx.navigation:navigation-ui-ktx:2.7.5'
    implementation 'androidx.navigation:navigation-compose:2.7.5'
    
    // Lottie for animations
    implementation 'com.airbnb.android:lottie:6.1.0'
    
    // Google Sign-In
    implementation 'com.google.android.gms:play-services-auth:20.7.0'
    
    // Browser for Apple Sign-In web flow
    implementation 'androidx.browser:browser:1.7.0'

    // Razorpay Checkout SDK
    implementation 'com.razorpay:checkout:1.6.33'

    // QR scanning (ZXing)
    implementation 'com.google.zxing:core:3.5.2'
    implementation 'com.journeyapps:zxing-android-embedded:4.3.0'
    
    // CameraX + ML Kit for license plate scanning
    implementation 'androidx.camera:camera-core:1.3.1'
    implementation 'androidx.camera:camera-camera2:1.3.1'
    implementation 'androidx.camera:camera-lifecycle:1.3.1'
    implementation 'androidx.camera:camera-view:1.3.1'
    implementation 'com.google.mlkit:text-recognition:16.0.1'
    
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
    androidTestImplementation platform('androidx.compose:compose-bom:2023.10.01')
    androidTestImplementation 'androidx.compose.ui:ui-test-junit4'
    debugImplementation 'androidx.compose.ui:ui-tooling'
    debugImplementation 'androidx.compose.ui:ui-test-manifest'
}
